#============================================================
#    Math Util
#============================================================
# - datetime: 2022-12-21 22:53:39
#============================================================
## 数学工具
class_name MathUtil


const INT_MIN : int = -2 ** 63 
const INT_MAX : int = 2 ** 63 - 1  #0x7FFFFFFFFFFFFFFF
const FLOAT_MIN : float = -1.79769e308
const FLOAT_MAX : float = 1.79769e308
const VECTOR2_MIN : Vector2 = -Vector2.INF
const VECTOR2_MAX : Vector2 = Vector2.INF
const VECTOR2I_MIN : Vector2i = -Vector2i(2 ** 31 - 1, 2 ** 31 - 1)
const VECTOR2I_MAX : Vector2i = Vector2i(2 ** 31 - 1, 2 ** 31 - 1)
#const INF = INF # 默认已有INF 无穷大 ∞


const DIRECTION_TO_NAME : Dictionary = {
	Vector2.UP + Vector2.LEFT: &"TOP_LEFT",
	Vector2.UP: &"TOP",
	Vector2.UP + Vector2.RIGHT: &"TOP_RIGHT",
	
	Vector2.ZERO: &"CENTER",
	Vector2.LEFT: &"LEFT",
	Vector2.RIGHT: &"RIGHT",
	
	Vector2.DOWN + Vector2.RIGHT: &"BOTTOM_RIGHT",
	Vector2.DOWN: &"BOTTOM",
	Vector2.DOWN + Vector2.LEFT: &"BOTTOM_LEFT",
}

const NAME_TO_DIRECTION : Dictionary = {
	# 顶部
	&"TOP_LEFT": Vector2.LEFT + Vector2.UP,
	&"TOP": Vector2.UP,
	&"TOP_RIGHT": Vector2.RIGHT + Vector2.UP,
	
	# 中间
	&"CENTER": Vector2.ZERO,
	&"ZERO": Vector2.ZERO,
	&"LEFT": Vector2.LEFT,
	&"RIGHT": Vector2.RIGHT,
	
	# 底部
	&"BOTTOM_RIGHT": Vector2.RIGHT + Vector2.DOWN,
	&"BOTTOM": Vector2.DOWN,
	&"BOTTOM_LEFT": Vector2.LEFT + Vector2.DOWN,
}

## 获取方向名称
static func get_direction_as_name(direction: Vector2) -> StringName:
	return DIRECTION_TO_NAME.get(direction, &"NULL")

## 获取这个名称的方向
static func get_direction_by_name(name: StringName) -> Vector2:
	return NAME_TO_DIRECTION.get(name, Vector2.INF)

static func distance_to(from: Vector2, to: Vector2) -> float:
	return from.distance_to(to)

static func distance_squared_to(from: Vector2, to: Vector2) -> float:
	return from.distance_squared_to(to)

static func direction_to(from: Vector2, to: Vector2) -> Vector2:
	return from.direction_to(to)

static func angle_to_point(from: Vector2, to: Vector2) -> float:
	return from.angle_to_point(to)

static func get_four_directions_i() -> Array[Vector2i]:
	return [Vector2i.LEFT, Vector2i.UP, Vector2i.RIGHT, Vector2i.DOWN]

static func get_eight_directions_i() -> Array[Vector2i]:
	return [Vector2i.LEFT, Vector2i(-1, -1), Vector2i.UP, Vector2i(1, -1), Vector2i.RIGHT, Vector2i(1, 1), Vector2i.DOWN, Vector2(-1, 1)]

static func get_four_directions() -> Array[Vector2]:
	return [Vector2.LEFT, Vector2.UP, Vector2.RIGHT, Vector2.DOWN]

static func get_eight_directions() -> Array[Vector2]:
	return [
		Vector2(-1, -1), Vector2.UP, Vector2(1, -1), #顶部从左到右
		Vector2.RIGHT, # 右
		Vector2(1, 1), Vector2.DOWN, Vector2(-1, 1), #底部从右到左
		Vector2.LEFT, # 左
	]

static func get_nine_grid_coords() -> Array[PackedVector2Array]:
	return [
		[Vector2i(-1, -1), Vector2i(0, -1), Vector2i(1, -1)],
		[Vector2i(-1, 0), Vector2i(0, 0), Vector2i(1, 0)],
		[Vector2i(-1, 1), Vector2i(0, 1), Vector2i(1, 1)],
	]

static func get_nine_directions() -> Array[Vector2]:
	return Array(DIRECTION_TO_NAME.keys(), TYPE_VECTOR2, "", null)

static func is_rect_edge(coord: Vector2, rect: Rect2, margin: float = 0) -> bool:
	return (
		coord.x == rect.position.x + margin 
		or coord.y == rect.position.y - 1 - margin
		or coord.x == rect.end.x - margin
		or coord.y == rect.end.y-1 + margin
	)

static func is_in_rect( coord: Vector2, rect: Rect2 ) -> bool:
	return rect.has_point(coord)


static func diff_length(velocity: Vector2, diff_length_value: float) -> Vector2:
	return velocity.limit_length(velocity.length() - diff_length_value)


static func is_in_range(value: float, min_value: float, max_value: float) -> bool:
	return value >= min_value and value <= max_value


## 参数来自节点类
class FromNode:
	
	static func diff_position(from: Node2D, to: Node2D) -> Vector2:
		return to.global_position - from.global_position
	
	static func distance_to(from: Node2D, to: Node2D) -> float:
		return from.global_position.distance_to(to.global_position)

	static func distance_squared_to(from: Node2D, to: Node2D) -> float:
		return from.global_position.distance_squared_to(to.global_position)

	static func direction_to(from: Node2D, to: Node2D) -> Vector2:
		return from.global_position.direction_to(to.global_position)

	static func angle_to(from: Node2D, to: Node2D) -> float:
		return from.global_position.angle_to(to.global_position)

	static func angle_to_point(from: Node2D, to: Node2D) -> float:
		return from.global_position.angle_to_point(to.global_position)
	
	static func direction_x(from: Node2D, to: Node2D) -> Vector2:
		return Vector2(to.global_position.x - from.global_position.x, 0)
	
	static func direction_y(from: Node2D, to: Node2D) -> Vector2:
		return Vector2(0, to.global_position.y - from.global_position.y)
	
	static func distance_x(from: Node2D, to: Node2D) -> float:
		return abs(from.global_position.x - to.global_position.x)
	
	static func distance_y(from: Node2D, to: Node2D) -> float:
		return abs(from.global_position.y - to.global_position.y)
	
	static func distance_v(from: Node2D, to: Node2D) -> Vector2:
		return (from.global_position - to.global_position).abs()
	
	static func is_in_distance(from: Node2D, to: Node2D, max_distance: float) -> bool:
		return from.global_position.distance_squared_to(to.global_position) <= pow(max_distance, 2)
	
	static func bounce_to( velocity: Vector2, from: Node2D, to: Node2D ):
		var dir = direction_to(from, to)
		return velocity.bounce(dir)
	
	##  移动后的向量
	##[br]
	##[br][code]target[/code]  目标节点
	##[br][code]vector[/code]  移动向量值
	##[br][code]return[/code]  返回移动后的位置
	static func move(target: Node2D, velocity: Vector2) -> Vector2:
		return target.global_position + velocity
	
	## 获取距离最近的节点
	static func get_closest_node(target_position: Vector2, nodes: Array) -> Node:
		if nodes.is_empty():
			return null
		nodes = nodes.filter(func(obj): return is_instance_valid(obj))
		if nodes.is_empty():
			return null
		if nodes.size() == 1:
			return nodes[0]
		var last_dist : float = INF
		var tmp_dist : float = 0.0
		var node : Node = null
		for child in nodes:
			tmp_dist = target_position.distance_squared_to(child.get_global_position())
			if last_dist > tmp_dist:
				last_dist = tmp_dist
				node = child
		return node


## 获取最近的点位置
static func get_closest_point_idx(from: Vector2, points: Array) -> int:
	if points.is_empty():
		return -1
	if points.size() == 1:
		return points[0]
	var last_dist : float = INF
	var tmp_dist : float = 0.0
	var closest_idx: int = -1
	for idx in points.size():
		tmp_dist = from.distance_squared_to(points[idx])
		if last_dist > tmp_dist:
			last_dist = tmp_dist
			closest_idx = idx
	return closest_idx


static func sum(list: Array) -> float:
	var i : float = 0
	for num in list:
		i += num
	return i

## 在距离之内
static func is_in_distance(from: Vector2, to: Vector2, max_distance: float) -> bool:
	return from.distance_squared_to(to) <= pow(max_distance, 2)


##  返回对应概率的值
##[br]
##[br][code]param[/code]  概率数据。value为随机值，key为对应的数据。示例：
##[codeblock]
##rand_probability({
##    "a": 0.3,  # 生成 a 的概率为 0.3/总数值
##    "b": 0.9,
##    "c": 0.15,
##    "d": 0.45,
##    "e": 1.2,
##    "f": 0.1,
##})
##[/codeblock]
static func rand_probability(param: Dictionary, random_number_generator: RandomNumberGenerator = null) -> Variant:
	return RandomProbabilityGenerator.new(param, random_number_generator).get_rand_value()


class _RandomVector2:
	var _origin: Vector2 # 原始位置
	var _random_number_generator: RandomNumberGenerator
	
	func _init(origin_pos: Vector2 = Vector2(0,0), random_number_generator: RandomNumberGenerator = null) -> void:
		self._origin = origin_pos
		self._random_number_generator = random_number_generator
	
	## 随机方向。from 开始角度，to 结束角度
	func rand_direction(from: float = -PI, to: float = PI) -> Vector2:
		return Vector2.LEFT.rotated( _random_number_generator.randf_range(from, to) if _random_number_generator else randf_range(from, to) )
	
	## 随机点位置
	## max_distance 随机的最大距离，min_distance 最小随机距离，
	## from_angle 开始角度，to_angle 到达角度
	func rand_point(max_distance: float, min_distance: float = 0.0, from_angle: float = -PI, to_angle: float = PI) -> Vector2:
		return _origin + rand_direction(from_angle, to_angle) * (_random_number_generator.randf_range(min_distance, max_distance) if _random_number_generator else randf_range(min_distance, max_distance))
	
	## 矩形内随机位置
	func rand_in_rect(rect: Rect2) -> Vector2:
		var x : float = _random_number_generator.randf_range( rect.position.x, rect.end.x ) if _random_number_generator else randf_range( rect.position.x, rect.end.x )
		var y : float = _random_number_generator.randf_range( rect.position.y, rect.end.y ) if _random_number_generator else randf_range( rect.position.y, rect.end.y )
		return _origin + Vector2(x, y)
	
	func rand_in_radius(radius: float) -> Vector2:
		return rand_point(radius, 0)


## 随机 Vector2 值
static func rand_vector2(origin_point: Vector2 = Vector2(0,0), random_number_generator: RandomNumberGenerator = null) -> _RandomVector2:
	return _RandomVector2.new(origin_point, random_number_generator)

## 位运算 - 存在于
static func bit_contain(number: int, is_in: int) -> bool:
	return (number & is_in) == number

## 位运算 - 相加
static func bit_add(list: Array) -> int:
	var v : int = 0
	for i in list:
		v |= i
	return v


## rect2 中随机一个位置
static func rand_position_in_rect2(rect: Rect2, random_number_generator: RandomNumberGenerator = null) -> Vector2:
	var x : float = random_number_generator.randf_range( rect.position.x, rect.end.x ) if random_number_generator else randf_range( rect.position.x, rect.end.x )
	var y : float = random_number_generator.randf_range( rect.position.y, rect.end.y ) if random_number_generator else randf_range( rect.position.y, rect.end.y )
	return Vector2(x, y)

static func rect2(size: Vector2, position: Vector2 = Vector2()) -> Rect2:
	return Rect2(position, size)

static func rect2i(size: Vector2i, position: Vector2i = Vector2i()) -> Rect2i:
	return Rect2i(position, size)

static func rect2_by_range(from: Vector2, to: Vector2) -> Rect2:
	return Rect2(from, to - from)

## 四周角落
static func quadrangle(rect: Rect2) -> Array[Vector2]:
	var top_left = rect.position
	var top_right = Vector2(rect.end.x, rect.position.y)
	var bottom_left = Vector2(rect.position.x, rect.end.y)
	var bottom_right = rect.end
	return Array([top_left, top_right, bottom_left, bottom_right], TYPE_VECTOR2, "", null)

static func quadranglei(rect: Rect2i) -> Array[Vector2i]:
	var top_left = rect.position
	var top_right = Vector2i(rect.end.x, rect.position.y)
	var bottom_left = Vector2i(rect.position.x, rect.end.y)
	var bottom_right = rect.end
	return Array([top_left, top_right, bottom_left, bottom_right], TYPE_VECTOR2I, "", null)


## 获取两个值的中间值
static func get_median_value(from, to):
	assert(typeof(from) == typeof(to), "两个参数的数据类型必须保持一致！")
	if (
		from is float
		or from is int
		or from is Vector2 
		or from is Vector2i
		or from is Vector3
		or from is Vector3i
		or from is Vector4
		or from is Vector4i
		or from is Color
	):
		return (from + to) / 2
	elif from is Rect2 or from is Rect2i:
		from.position += to.position
		from.size += to.size
		
		from.position /= 2
		from.size /= 2
		return from
		
	else:
		assert(false, "不支持的数据类型")


static func is_number(value) -> bool:
	return value is float or value is int


## 找到其中最大的 x 和 y 后的 Vector2
static func get_max_xy(list: Array) -> Vector2:
	var max_v = -Vector2.INF
	for item in list:
		max_v.x = max(max_v.x, item.x)
		max_v.y = max(max_v.y, item.y)
	return max_v

## 找到其中最小的 x 和 y 后的 Vector2
static func get_min_xy(list: Array) -> Vector2:
	var min_v = Vector2.INF
	for item in list:
		min_v.x = min(min_v.x, item.x)
		min_v.y = min(min_v.y, item.y)
	return min_v

static func get_min_xy_by_dist(list: Array, point: Vector2) -> Vector2:
	assert(not list.is_empty(), "列表项不能为空")
	var curr : Vector2
	var last_dist = INF
	var dist = INF
	for item in list:
		dist = Vector2(item).distance_squared_to(point)
		if last_dist > dist:
			last_dist = dist
			curr = Vector2(item)
	return curr

static func get_max_xy_by_dist(list: Array, point: Vector2) -> Vector2:
	assert(not list.is_empty(), "列表项不能为空")
	var curr : Vector2
	var last_dist = INF
	var dist = INF
	for item in list:
		dist = Vector2(item).distance_squared_to(point)
		if last_dist < dist:
			last_dist = dist
			curr = item
	return curr

## 根据 Vector2 列表中最大和最小的位置，返回 Rect2
static func get_rect_by_max_min_vector2(list: Array) -> Rect2:
	var min_v = get_min_xy(list)
	var max_v = get_max_xy(list)
	return Rect2(min_v, max_v - min_v)

static func rotated(vector: Vector2, angle: float) -> Vector2:
	return vector.rotated(angle)

## 合并 Rect
static func merge_rect(a: Rect2, b: Rect2) -> Rect2:
	var position = MathUtil.get_min_xy([a.position, b.position])
	var end = MathUtil.get_max_xy([a.end, b.end])
	return Rect2(position, end - position)
